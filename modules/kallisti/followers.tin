#nop -- follower management

#var kallisti/followers-description {Follower / Group Management}
#var kallisti/followers-help {
Recharm followers, convenience commands.
}

#nop --
#nop Modloader Stuff
#nop --

#alias kallisti/followers-register {
	#if {@isloaded{modloader}} {
		register_module kallisti/followers
	} {
		fail_module kallisti/followers unknown reason
	}
}

init follower_mode preferences on
init toggle[recallwithleader] preferences on
init toggle[portalwithleader] preferences on

event_register {commsGroup} {followers} {follower_command {%2} {%3} {%4}};

#alias {thumbout %1} {
    #line sub vars #act {^%1 appears in the middle} {fol %1;#unact {^%1 appears in the middle}}
}

#alias follower_command {
    #if {"%2" == "tnl"} {tnl};
    #if {"%2" == "all tnl"} {tnl};
    #if {"%1" == "@group_leader{}" && "$follower_mode" == "on"} {
        #regex {%2} {^all %*$} {
            #send &1;
        }
    } 
}

#alias oo {order followers %0}

#action {^%w disappears.$} {
    #if {"%1" == "@group_leader{}" && "$toggle[recallwithleader]" == "on"} {
        recall;
        #if {&recall_special[@group_leader{}]} {$recall_special[@group_leader{}]}
    };
}

#action {^%w enters %2$} {
    #var _portal %2;
    #switch {"%2"} {
        #case {"an earthen-tone portal."} {#var _portal {portal}};
        #case {"a glowing blue-flame portal."} {#var _portal {portal}};
        #case {"a spider-silkwool Zarkan tent."} {#var _portal {tent}};
        #default {
            #replace {_portal} {%w } {};
            #replace {_portal} {.} {};
        }
    };
	#if {"%1" == "@group_leader{}" && "toggle[portalwithleader]" == "on"} {
		warn follower attempt to enter $_portal;
		enter $_portal;
	}
}

#action {^%w turns %2$} {

    #var _portal %2;
    
    #switch {"%1"} {
        #case {"Fortune"} {#return};
    };

    #switch {"%2"} {
        #case {"translucent."} {#return};
        #default {
            #replace {_portal} {%w } {};
            #replace {_portal} {.} {};
        };
    };

    warn follower %1 instructed to turn $_portal;
    #if {"%1" == "@group_leader{}" && "toggle[portalwithleader]" == "on"} {
        turn $_portal;
    }
}

#action {^Renewing charm duration for %1 to %d hours.$} {
    #untick {charmm failer};
    #math {cdelay} { ( %2 - 2 ) * 75 };
    #class kallisti/followers assign #line oneshot #tick {recharm} {#class kallisti/followers assign #tick {charmm failer} {charm_group} {20}} {$cdelay}
}

#alias charm_group {
    #switch {"$MSDP_CLASS"} {
        #case {"Druid"} {nco friend group};
        #default {err No recharm method for $MSDP_CLASS};
    };
}

#function groupAmILeader {                                                                                
    #var result 0;
    #loop {1} {&MSDP_GROUP[]} {index} {
        #if { "$MSDP_GROUP[$index][name]" == "$MSDP_CHARACTER_NAME" } {
            #if {$MSDP_GROUP[$index][is_leader] == 1} { #return 1 }
        }
    };
    #return 0
}

#function group_leader {
    #loop {1} {&MSDP_GROUP[]} {index} {
        #if {$MSDP_GROUP[$index][is_leader] == 1} {#return $MSDP_GROUP[$index][name]}
    };
    #return 0
}

