#nop --
#nop Class Definitions
#nop --

#var kallisti-description {Login and session management for Legends of Kallisti}
#var kallisti-help {Use connect to connect to Legends of Kallisti on the main port.}

#nop --
#nop Modloader Stuff
#nop --

#alias kallisti-register {
	#if {@isloaded{events}} {
		register_module kallisti
	} {
		fail_module kallisti
	}
}

#tab connect
#alias connect {
	#showme attempt to create session for player %1.;
	#switch {"%2"} {
		#case {"tp"} {
			#session kallisti:%1 {kallistimud.com} {4069} {$players/%1_kallisti.player};
		};
		#case {"builder"} {
			#session kallisti:%1 {kallistimud.com} {4005} {$players/%1_kallisti.player};
		};
		#default {
			#session kallisti:%1 {kallistimud.com} {4000} {$players/%1_kallisti.player};
		};
	};
	#if {"%1" != "${char_name}"} {
	    warn CONFIG '%1' does not match '${char_name}' in $players/%1_kallisti.player;
        #format {_ts} {%t} {%D %H:%m:%S};
	    #line log $log/tth.error ${_ts} warn CONFIG '%1' does not match char_name field '${char_name}' in $players/%1_kallisti.player;

    };
}

#alias post_connect_load {
    #showme %0;
	info Loading post-connect modules;
	load_module prefs;
	load_module layout;
	load_module kal_comms;
	load_module map;
	load_module queues;
	#foreach {$modules_on_connect[%*]} {module} {
			load_module $module
	};

	info kallisti Loading kallisti modules;
	#foreach {autobuf;levels;meta;odometer;playermap;followers} tmp {
		load_module kallisti/$tmp
	};
	#line oneshot #tick {clear} {#buffer end} {0.5};
	#if {@is_alias{player_setup}} {player_setup %1};
}

#act {^Enter your account name.} {
	#send ${account_name};
	#class account read {$players/${account_name}_kallisti.account}
}

#act {^Please enter your account password (or just press <ENTER> to abort login):} {
	#send ${account_password};
	#if {"${account_dont_forget_password}" != "true"} {
		#showme <fcc>Forgetting password.;
		#unvar account_password;
	};
}

#act {^                             *** PRESS ENTER ***} {#send motd;#send 3 $char_name;#send 1}

#act {^Welcome to the legendary lands of Kallisti.} {post_connect_load login}
#act {^Reconnecting.} {post_connect_load reconnect}


#nop -- Let's populate two tiles as samples as they're commonly asked for;
event_register {variable MSDP_AREA_NAME} {right} {generate_area_info {%0} {%1} {%2}};
event_register {variable MSDP_GROUP} {right} {generate_group_info {%0} {%1} {%2}};

#alias generate_group_info {
    syslog right debug generate new group info - %2;
    #if {(!&{MSDP_GROUP}) || (&MSDP_GROUP[] == 0)} {
		#list group_info create {<Ffff>Not Grouped};
    } {
    	#list group_info create {<Ffff>Group (level ${MSDP_GROUPLEVEL})};
	};
    #foreach *MSDP_GROUP[%*] {_idx} {
		#var _flags $MSDP_GROUP[$_idx][flags];
		#replace {_flags} {[ranged]} {>};
		#local _lev $MSDP_GROUP[$_idx][level];
		#if {$_lev > 99} {
			#math {_lev} {$_lev - 100};
			#var _lev *${_lev};
		};

        #format _line {<ffff>%-5s%+2s] %-10.9s %-3s %-3s %-3s <ffff>%-4s}
            {@withMe{$_idx}[$MSDP_GROUP[$_idx][class]}
			{$_lev}
            {$MSDP_GROUP[$_idx][name]}
            {@colorPct{$MSDP_GROUP[$_idx][health]}}
            {@colorPct{$MSDP_GROUP[$_idx][mana]}}
            {@colorPct{$MSDP_GROUP[$_idx][stamina]}}
            {$_flags};
             #list group_info add {{$_line}};
    };
    #if {@is_alias{display_right_tiles}} {display_right_tiles}
}

#alias generate_area_info {
    syslog right debug generate new area info - %1|%2|%3;
    #var area_info {${MSDP_AREA_NAME}\nLevel: $MSDP_AREA_MINLEVEL - $MSDP_AREA_MAXLEVEL};
    #if {@is_alias{display_right_tiles}} {display_right_tiles}
}

#nop -- generate_area_info;
#nop -- generate_group_info;

#tick {idle} {#cr} {30}

#action {^You have no knowledge of the spell, '%1'.} {event_raise {unlearned spell} {%1}}
#act {^You receive your reward for the kill, %1 experience points.} {
    #buffer get _ripline -1;
    #var _xp %1;
    #regex {$_ripline} {{.*} is dead!} {
        event_raise {mob kill} {&1} $_xp
    } {
        event_raise {mob kill} {unknown} $_xp
    }
}

#nop -- only one of the following is required, figure out which
#action {~^\e[0m\e[0;32m< {\w+} |} {
    event_raise {prompt} {%1}
}
#action {^< $MSDP_CHARACTER_NAME %1>$} {event_raise prompt}
#action {^{[a-zA-Z ]+} {rides|flies|sneaks} in} {event_raise {entity arrival} {%1}}

#action {^%w starts following you.$} {event_raise {new follower} {%1}}

#nop vim: syntax=tt
