#nop --
#nop Class Definitions
#nop --

#var kal_comms-description {LoK communications logging support}
#var kal_comms-help {coming soon}

#nop --
#nop Modloader Stuff
#nop --

#alias kal_comms-register {
	#if {@isloaded{events}} {
		register_module kal_comms
	} {
		#showme ERROR: events not loaded;
		fail_module kal_comms
	}
}

#alias kal_comms-unregister {
	#zap ${comms-session}
}

#nop --
#nop Initialize some port stuff
#nop --
init comms-session comms;

#info sessions save;
#if {!&info[SESSIONS][${comms-session}]} {
	#line quiet #port {init} {${comms-session}} {0};
	#untick {idle};
	#ses -;
}

#${comms-session} #macro {OP} {#ses kallisti:${char_name}};
#${comms-session} #event {SESSION ACTIVATED} {#buffer end};
#gts event_register {SESSION DISCONNECTED kallisti:${char_name}} {kal_comms} {#zap ${comms-session}}

#macro {OQ} {#ses ${comms-session}};

#alias kal_comms-unregister {
	event_remove_handler kal_comms;
}

#var chat[rows] 10;


#alias {update_chat} {
	#var _msg %2;
	#replace {_msg} {{} {\{};
	#replace {_msg} {}} {\}};

	#if {&chat[log][] >= $chat[rows]} {
		#list chat[log] del 1
	};
	#list chat[log] ins -1 {$_msg};
	#if {@isloaded{layout}} {
		layout_comms_update
	};
	send_to_comms_session $_msg
}

#alias {send_to_comms_session} {
	#info sessions save;
	#if {&info[SESSIONS][${comms-session}]} {
		#${comms-session} #showme %0
	}
}

#nop -- ---------------------------------------------------------------
#nop -- Group communications
#nop -- ---------------------------------------------------------------
#action {~^\e[1;36mYou grouptell: {.*}} {
    event_raise {commsGroup} {$name} {%1} {%0};
} {8}
#action {~^\e[1;36m**%w: '%2'\e[0m$} {
   event_raise {commsGroup} {%1} {%2} {%0};
} {8}

#action {~^\e[1;36m<Clan: %w> '{.*}'} {
    event_raise {commsClan} {%1} {%2} {%0};
} {8}
#action {~^\e[1;36mYou cchat, '{.*}'} {
    event_raise {commsClan} {$name} {%1} {%0};
} {9}

#action {~^\e[1;35m<Gemote> '{.*}' } {
	event_raise {commsGemote} {} {%1} {%0};
}

#action {~^\e[1;35m<Gossip: %w> '{.*}'\e[0m$} {
    event_raise {commsGos} {%1} {%2} {%0};
} {3}
#action {~^\e[1;35m<Gossip: %w ({.*})> '{.*}'\e[0m$} {
    event_raise {commsGos} {%1 (%2)} {%3} {%0};
} {4}

#action {~^\e[1;35m<Gemote> '%1'\e[0m$} {
    event_raise {commsGemote} {%1} {%2} {%0};
}

#action {~^\e[0;36m<Development: %w> '%2'\e[0m$} {
    event_raise {commsDev} {%1} {%2} {%0};
} {8}
#action {~^\e[0;36m<Development: %w ({.*})> '%3'\e[0m$} {
    event_raise {commsDev} {%1 (%2)} {%3} {%0};
} {7}

#action {~^\e[0;36m<Advice: %w> '%2'$} {
    event_raise {commsDev} {%1} {%2} {%0};
} {8}
#action {~^\e[0;36m<Advice: %w (%w)> '%3'$} {
    event_raise {commsDev} {%1 (%2)} {%3} {%0};
} {7}


#action {~^\e[1;36m%w ({.*}) tells you, '{.*}'\e[0m$$} {
    event_raise {commsTell} {%1 (%2)} {%3} {%0};
} {8}
#action {~^\e[1;36mYou tell {.*}, '{.*}'\e[0m$} {
    event_raise {commsTell} {me (to %1)} {%2} {%0};
} {8}

#action {^The winds whisper, '{.*}'} {
    event_raise {commsWorld} {%1} {%0};
} {9}

#action {~^\e[1;36m[%w:(%w)] {.*}} {
	event_raise {commsChat} {%2} {%0};
} {9}
#action {~^\e[1;36m[%w:] {.*}} {
	event_raise {commsChat} {%2} {%0};
} {9}

#action {^You say, '%1'$} {
	event_raise {commsSay} {You} {%1} {%0};
}
#action {^%w {says|grunts}, '%1'$} {
	event_raise {commsSay} {%1} {%3} {%0};
}

#action {^<Hero: %w ({.*})> '%3'$} {
	event_raise {commsHero} {%1 (%2)} {%3} {%0};
}

#action {^<Hero: %w> '{.*}'$} {
	event_raise {commsHero} {%1} {%3} {%0};
}

#action {^<Advice: %w ({.*})> '{.*}'$} {
	event_raise {commsAdvice} {%1 (%2)} {%3} {%0};
}

#action {^<Advice: %w> '{.*}'$} {
	event_raise {commsAdvice} {%1} {%3} {%0};
}


#nop -- ---------------------------------------------------------------
#nop -- Events
#nop -- ---------------------------------------------------------------
event_register {commsGroup} {kal_comms} {update_chat group {%4}};
event_register {commsClan}  {kal_comms} {update_chat clan {%4}};
event_register {commsGos}   {kal_comms} {update_chat gos {%4}};
event_register {commsGemote}{kal_comms} {update_chat gemote {%4}};
event_register {commsTell}  {kal_comms} {update_chat tell {%4}};
event_register {commsWorld} {kal_comms} {update_chat world {%3}};
event_register {commsDev}   {kal_comms} {update_chat dev {%4}};
event_register {commsChat}  {kal_comms} {update_chat chat {%3}};
event_register {commsSay}   {kal_comms} {update_chat say {%4}};
event_register {commsHero}  {kal_comms} {update_chat hero {%4}};
event_register {commsAdvice}{kal_comms} {update_chat advice {%4}};

#nop -- vim: syntax=tt
